`default_nettype none

module fdiv (
    input  wire logic        clk,
    input  wire logic        rst_n,
    input  wire logic [31:0] input_a,
    input  wire logic [31:0] input_b,
    input  wire logic        input_valid,
    output logic [31:0]      result,
    output logic             out_valid   
);

    localparam [31:0] QUIET_NAN = 32'h7FC0_0000;

    wire s_a = input_a[31];
    wire [7:0] e_a = input_a[30:23];
    wire [23:0] m_a = (|e_a) ? {1'b1, input_a[22:0]} : {1'b0, input_a[22:0]};

    wire s_b = input_b[31];
    wire [7:0] e_b = input_b[30:23];
    wire [23:0] m_b = (|e_b) ? {1'b1, input_b[22:0]} : {1'b0, input_b[22:0]};

    wire [7:0] calc_e_a = (|e_a) ? e_a : 8'd1;
    wire [7:0] calc_e_b = (|e_b) ? e_b : 8'd1;

    wire a_is_nan = (&e_a) && (input_a[22:0] != 0);
    wire b_is_nan = (&e_b) && (input_b[22:0] != 0);
    wire a_is_inf = (&e_a) && (input_a[22:0] == 0);
    wire b_is_inf = (&e_b) && (input_b[22:0] == 0);
    wire a_is_zero = input_a[30:0] == 0;
    wire b_is_zero = input_b[30:0] == 0;

    (* ram_style = "block" *)reg [47:0] lut [0:4095]; 
    initial begin
        $readmemh("taylor_lut.mem", lut);
    end

    reg st1_valid, st1_sign, st1_nan;
    reg [24:0] st1_adjusted_a;
    reg [10:0] st1_m_b_10_0;
    reg [47:0] st1_y0_dy;
    reg [48:0] st1_bias;

    reg [7:0] st1_exp_C, st1_exp_N, st1_exp_S;
    reg st1_inf_C, st1_inf_N, st1_inf_S;
    reg st1_zero_C, st1_zero_N, st1_zero_S;

    wire shift_pred = (m_a >= m_b);
    wire [24:0] adjusted_a = shift_pred ? {1'b0, m_a} : {m_a, 1'b0};

    wire signed [10:0] raw_exp = {3'b0, calc_e_a} - {3'b0, calc_e_b} + 11'sd127;
    wire signed [10:0] base_exp = shift_pred ? raw_exp : raw_exp - 11'sd1;
    
    wire signed [10:0] exp_C = base_exp + 11'sd1; 
    wire signed [10:0] exp_N = base_exp;          
    wire signed [10:0] exp_S = base_exp - 11'sd1; 

    wire ovf_C = (exp_C >= 11'sd255); wire udf_C = (exp_C <= 11'sd0);
    wire ovf_N = (exp_N >= 11'sd255); wire udf_N = (exp_N <= 11'sd0);
    wire ovf_S = (exp_S >= 11'sd255); wire udf_S = (exp_S <= 11'sd0);

    wire is_nan = a_is_nan || b_is_nan || (a_is_zero && b_is_zero) || (a_is_inf && b_is_inf);
    wire is_inf = a_is_inf || b_is_zero;
    wire is_zero = a_is_zero || b_is_inf;

    always @(posedge clk) begin
        st1_y0_dy <= lut[m_b[22:11]]; 
    end

    always @(posedge clk or negedge rst_n) begin
        if (!rst_n) begin
            st1_valid <= 1'b0; st1_sign <= 1'b0; st1_nan <= 1'b0;
            st1_adjusted_a <= 25'b0; st1_m_b_10_0 <= 11'b0;
            st1_bias <= 49'b0;
            
            st1_exp_C <= 8'b0; st1_exp_N <= 8'b0; st1_exp_S <= 8'b0;
            st1_inf_C <= 1'b0; st1_inf_N <= 1'b0; st1_inf_S <= 1'b0;
            st1_zero_C <= 1'b0; st1_zero_N <= 1'b0; st1_zero_S <= 1'b0;
        end else begin
            st1_valid <= input_valid;
            st1_sign <= s_a ^ s_b;
            st1_nan <= is_nan;

            st1_adjusted_a <= adjusted_a;
            st1_m_b_10_0 <= m_b[10:0];

            st1_bias <= (m_b[22:0] == 23'b0) ? 49'h0 : 49'h0_0000_017F_FFFF;

            st1_exp_C <= exp_C[7:0];
            st1_exp_N <= exp_N[7:0];
            st1_exp_S <= exp_S[7:0];

            st1_inf_C <= (is_inf && !is_nan) || ovf_C;
            st1_inf_N <= (is_inf && !is_nan) || ovf_N;
            st1_inf_S <= (is_inf && !is_nan) || ovf_S;

            st1_zero_C <= (is_zero && !is_nan) || udf_C;
            st1_zero_N <= (is_zero && !is_nan) || udf_N;
            st1_zero_S <= (is_zero && !is_nan) || udf_S;
        end
    end

    reg st2_valid, st2_sign, st2_nan;
    reg [24:0] st2_adjusted_a;
    reg [23:0] st2_x1;
    reg [48:0] st2_bias;

    reg [7:0] st2_exp_C, st2_exp_N, st2_exp_S;
    reg st2_inf_C, st2_inf_N, st2_inf_S;
    reg st2_zero_C, st2_zero_N, st2_zero_S;

    wire [47:0] stage2_C = {1'b0, st1_y0_dy[47:24], 23'b0}; // y_0
    wire [34:0] mult2    = st1_y0_dy[23:0] * st1_m_b_10_0; // dy * delx
/* verilator lint_off UNUSEDSIGNAL */
    wire [47:0] stage2_P = stage2_C - {13'b0, mult2};               
/* verilator lint_on UNUSEDSIGNAL */
    wire [23:0] x1_taylor = stage2_P[46:23]; // the most pricise part of fixed point 

    always @(posedge clk or negedge rst_n) begin
        if (!rst_n) begin
            st2_valid <= 1'b0;
            st2_sign <= 1'b0; st2_nan <= 1'b0; 
            st2_adjusted_a <= 25'b0; st2_x1 <= 24'b0; st2_bias <= 49'b0;
            
            st2_exp_C <= 8'b0;
            st2_exp_N <= 8'b0; st2_exp_S <= 8'b0;
            st2_inf_C <= 1'b0; st2_inf_N <= 1'b0; st2_inf_S <= 1'b0;
            st2_zero_C <= 1'b0;
            st2_zero_N <= 1'b0; st2_zero_S <= 1'b0;
        end else begin
            st2_valid <= st1_valid;
            st2_sign <= st1_sign; 
            st2_nan <= st1_nan; 

            st2_adjusted_a <= st1_adjusted_a;
            st2_x1 <= x1_taylor;
            st2_bias <= st1_bias;
            
            st2_exp_C <= st1_exp_C;
            st2_exp_N <= st1_exp_N; st2_exp_S <= st1_exp_S;
            st2_inf_C <= st1_inf_C; st2_inf_N <= st1_inf_N; st2_inf_S <= st1_inf_S;
            st2_zero_C <= st1_zero_C;
            st2_zero_N <= st1_zero_N; st2_zero_S <= st1_zero_S;
        end
    end

    wire [41:0] mult3_lo = st2_adjusted_a * st2_x1[16:0];  
    wire [31:0] mult3_hi = st2_adjusted_a * st2_x1[23:17]; 
    
/* verilator lint_off UNUSEDSIGNAL */
    wire [48:0] q_final  = {mult3_hi, 17'b0} + {7'b0, mult3_lo} + st2_bias;
/* verilator lint_on UNUSEDSIGNAL */

    wire carry = q_final[47]; 
    wire norm  = q_final[46]; 

    wire [22:0] final_fraction = carry ? q_final[46:24] :
                                 norm  ? q_final[45:23] :
                                         q_final[44:22];

    wire [7:0] final_exp = carry ? st2_exp_C :
                           norm  ? st2_exp_N :
                                   st2_exp_S;

    wire out_is_inf  = carry ? st2_inf_C : (norm ? st2_inf_N : st2_inf_S);
    wire out_is_zero = carry ? st2_zero_C : (norm ? st2_zero_N : st2_zero_S);

    always @(posedge clk or negedge rst_n) begin
        if (!rst_n) begin
            result <= 32'b0;
            out_valid <= 1'b0;
        end else begin
            out_valid <= st2_valid;
            if (st2_valid) begin
                if (st2_nan) begin
                    result <= QUIET_NAN;
                end else if (out_is_inf) begin
                    result <= {st2_sign, 8'hFF, 23'b0};
                end else if (out_is_zero) begin
                    result <= {st2_sign, 31'b0};
                end else begin
                    result <= {st2_sign, final_exp, final_fraction};
                end
            end
        end
    end

endmodule
`default_nettype wire
